plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.example.multiflavourdemoapp"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "dimention"
    productFlavors {
        firebase {
            dimension "dimention"
            applicationId "com.example.multiflavourdemoapp.firebase"
        }

        jpush {
            dimension "dimention"
            applicationId "com.example.multiflavourdemoapp.jpush"

            ndk {
                abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86'
            }

            manifestPlaceholders = [
                    JPUSH_PKGNAME: applicationId,
                    JPUSH_APPKEY : "2ab3a511a314d89255eac9d6",
                    JPUSH_CHANNEL: "developer-default",
            ]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    // Firebase
    firebaseImplementation platform('com.google.firebase:firebase-bom:29.2.1')
    firebaseImplementation 'com.google.firebase:firebase-messaging-ktx'
    firebaseImplementation 'com.google.firebase:firebase-analytics'

    // JPush
    jpushImplementation 'cn.jiguang.sdk:jpush:4.6.2'
    jpushImplementation 'cn.jiguang.sdk:jcore:3.2.2'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Firebase")) {
    println("Building flavor with Google GCM - applying plugin")
    apply plugin: 'com.google.gms.google-services'
}